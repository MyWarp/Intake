buildscript {
    repositories {
        jcenter()
    }

    configurations.all {
        resolutionStrategy {
            force 'com.google.guava:guava:18.0'
        }
    }

    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
    }
}

//build variables
if (!project.hasProperty("webdav_url")){
    ext.webdav_url = "http://localhost"
}
if (!project.hasProperty("webdav_user")){
    ext.webdav_user = "guest"
}
if (!project.hasProperty("webdav_password")){
    ext.webdav_password = ""
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'checkstyle'

    group = 'io.github.mywarp.intake'
    version = '4.2-SNAPSHOT'

    checkstyle.configFile = new File(rootProject.projectDir, "config/checkstyle/checkstyle.xml")

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        jcenter()
        maven { url "http://maven.sk89q.com/repo/" }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    build.dependsOn(checkstyleMain)
    build.dependsOn(checkstyleTest)
    build.dependsOn(sourcesJar)
    build.dependsOn(javadocJar)

    configurations {
        deployerJars
    }

    dependencies {
        // to use WebDav protocol on upload
        deployerJars 'org.apache.maven.wagon:wagon-webdav:1.0-beta-2'
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                configuration = configurations.deployerJars
                repository(url: "dav:" + webdav_url) {
                    authentication(userName: webdav_user, password: webdav_password)
                }
            }
        }
    }
}
